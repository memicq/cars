plugins {
	id 'org.springframework.boot' version '2.4.0'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id 'idea'
}

group = 'com.tetete'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

idea {
	module {
		inheritOutputDirs = false
		outputDir = file("$buildDir/classes/main/")
	}
}

repositories {
	mavenCentral()
}

dependencyManagement {
	imports {
		mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
	}
}

dependencies {
	def lombokVersion = "1.18.6"

	implementation 'org.springframework.boot:spring-boot-starter-web'
	runtimeOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// for lombok
	// testAnnotationProcessor、testCompileOnly を併記しなくてよいよう configurations で設定している
	annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
	compileOnly("org.projectlombok:lombok:${lombokVersion}")
}

bootRun {
	jvmArgs = ['-Dspring.profiles.active=development']
}

clean.delete fileTree("src/main/resources/static").include("**/*")
task copyDistToStatic(type: Copy) {
	from project(":carweb").file("dist")
	into "src/main/resources/static"
}

copyDistToStatic.dependsOn ":carweb:build"
processResources.dependsOn copyDistToStatic